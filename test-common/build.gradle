plugins {
    id 'java-library'
    alias(libs.plugins.kotlin.jvm)
    id 'maven-publish'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

kotlin {
    explicitApi()
}

dependencies {
    api libs.kotest.runner.junit5.jvm
    api libs.mockk
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/GaurangShaha/BuildingBlocks"
            credentials {
                def propertiesFile = new File('local.properties')
                if (propertiesFile.exists()) {
                    Properties props = new Properties()
                    props.load(new FileInputStream(propertiesFile))
                    username props.getProperty("gpr.user")
                    password props.getProperty("gpr.key")
                } else {
                    username System.getenv("GITHUB_ACTOR")
                    password System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
            groupId = "android.artisan"
            artifactId = "test-common"
            version = "1.0.1"
        }
    }
}